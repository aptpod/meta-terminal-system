require conf/distro/poky.conf

DISTRO = "terminal-system-os-2"
DISTRO_NAME = "Terminal System OS 2 (Yocto Project Distro)"
DISTRO_VERSION = "${TS_DISTRO_VERSION}"
DISTRO_VERSION_BUILD_METADATA = "${@bb.utils.contains('TS_DISTRO_VERSION', 'develop', \
                                    '+${TS_DISTRO_VERSION_BUILD_METADATA}' if d.getVar('TS_DISTRO_VERSION_BUILD_METADATA') else '+${DATETIME}', \
                                    '+${TS_DISTRO_VERSION_BUILD_METADATA}' if d.getVar('TS_DISTRO_VERSION_BUILD_METADATA') else '', d)}"
DISTRO_VERSION_BUILD_METADATA[vardepsexclude] += "TS_DISTRO_VERSION_BUILD_METADATA DATETIME"

DISTRO_FEATURES:append = " systemd virtualization"
VIRTUAL-RUNTIME_init_manager = "systemd"
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit"
# avoid conflicts between avahi-daemon and systemd-resolved for mDNS support
DISTRO_FEATURES:remove = " zeroconf"

INHERIT += "extrausers"
EXTRA_USERS_PARAMS = "usermod root"

VOLATILE_LOG_DIR = "no"

PACKAGE_CLASSES ?= "package_deb"

TS_RESOURCES_DIR ?= "${HOME}"

# Used in docker-ce_%.bbappend to install docker-compose-plugin
HOSTTOOLS += "docker"
# Used in docker-archive-load.bb to install pre-install docker image
HOSTTOOLS += "aws jq"
